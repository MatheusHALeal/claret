/*
 * This is an example claret specification
 */
 
systemName "Ifood"

usecase "Realizar pedido" {

  version "0.1" type "criação do caso de uso" user "set_user" date "17/09/2019"
  version "0.2" type "atualização" user "set_user" date "18/09/2019"
  actor appUser "Usuário do app"
  
  preCondition "Existe uma conexão de rede", "O usuário deve possuir uma conta", "O usuário deve estar logado"
  
  basic {
    step 1 appUser "Acessa a lista geral de restaurantes" af[1, 3, 2]
    step 2 system "Mostra a lista com todos os restaurantes"
    step 3 appUser "Escolhe o restaurante desejado" 
    step 4 system "Mostra cardápio do restaurante" ef[3]
    step 5 appUser "Clica no prato escolhido"
    step 6 system "Mostra tela de pagamento" ef[2]
    step 7 appUser "Escolhe forma de pagamento via cartão" af[4]
    step 8 system "Confirma o pagamento e exibe tela de confirmação de endereço" ef[1]
    step 9 appUser "Confirma endereço"
    step 10 system "Exibe tela de pedido realizado com sucesso"
    
  }
  alternative 1 "Usuário acessa restaurantes favoritos" {
  	step 1 appUser "Acessa a lista dos restaurantes favoritos" bs 2
  }
  
  alternative 2 "Usuário acessa lista dos últimos restaurantes onde realizou pedido" {
  	step 1 appUser "Acessa a lista dos últimos restaurantes onde realizou pedido"  bs 2
  }
  
  alternative 3 "Usuário acessa lista dos últimos pedidos" {
  	step 1 appUser "Acessa lista dos últimos pedidos feitos"  
  	step 2 system "Mostra a lista dos últimos pedidos feitos"
  	step 3 appUser "Clica no prato escolhido" bs 6
  }
  
  alternative 4 "Usuário paga através de dinheiro" {
  	step 1 appUser "Paga com dinheiro" 
  	step 2 system "Confirma o pagamento e exibe tela de confirmação de endereço" 
  	step 3 appUser "Confirma endereço" bs 10
  }

  exception 1 "Forma de pagamento não autorizada" {
  	step 1 system "Mostra mensagem de forma de pagamento não autorizada" bs 6
  }
  
  exception 2 "Prato não disponível" {
  	step 1 system "Mostra mensagem de prato indisponível" bs 4
  }
  
  exception 3 "Restaurante fechado" { 
  	step 1 system "Mostra mensagem de restaurante fechado" bs 1
  }
  
  postCondition "Pedido realizado"
}